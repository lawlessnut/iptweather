ShowWaitCursor
#ErrorLevel("off")

# ############################################################################
# retrieve user preference
IPTWforecastStartIcon				= IniRead("weather.ini", "User settings", "IPTWforecastStartIcon")
IPTWforecastStartPage				= IniRead("weather.ini", "User settings", "IPTWforecastStartPage")
IPTWcurrentWeatherIcon				= IniRead("weather.ini", "User settings", "IPTWcurrentWeatherIcon")
IPTWcurrentWeatherPage				= IniRead("weather.ini", "User settings", "IPTWcurrentWeatherPage")
IPTWiconsExtension					= IniRead("weather.ini", "User settings", "IPTWiconsExtension")
IPTWgoToAccuWeatherQuestion			= IniRead("weather.ini", "User settings", "IPTWgoToAccuWeatherQuestion")
weekday["Monday"]					= IniRead("weather.ini", "User settings", "IPTWmondayText")
weekday["Tuesday"]					= IniRead("weather.ini", "User settings", "IPTWtuesdayText")
weekday["Wednesday"]				= IniRead("weather.ini", "User settings", "IPTWwednesdayText")
weekday["Thursday"]					= IniRead("weather.ini", "User settings", "IPTWthursdayText")
weekday["Friday"]					= IniRead("weather.ini", "User settings", "IPTWfridayText")
weekday["Saturday"]					= IniRead("weather.ini", "User settings", "IPTWsaturdayText")
weekday["Sunday"]					= IniRead("weather.ini", "User settings", "IPTWsundayText")
IPTWobsoleteDataPrefix				= IniRead("weather.ini", "User settings", "IPTWobsoleteDataPrefix")
IPTWupdateWeatherNowQuestion		= IniRead("weather.ini", "User settings", "IPTWupdateWeatherNowQuestion")
IPTWdisplayCityName					= IniRead("weather.ini", "User settings", "IPTWdisplayCityName")
IPTWdisplayObservationTime			= IniRead("weather.ini", "User settings", "IPTWdisplayObservationTime")
IPTWforceDelayHours					= IniRead("weather.ini", "User settings", "IPTWforceDelayHours")
IPTWforceDelayMinutes				= IniRead("weather.ini", "User settings", "IPTWforceDelayMinutes")
IPTWnightIconSuffix					= IniRead("weather.ini", "User settings", "IPTWnightIconSuffix")
IPTWdownloadDataAfterStartOnIconTap	= IniRead("weather.ini", "User settings", "IPTWdownloadDataAfterStartOnIconTap")
IPTWaccuWeatherLocation				= IniRead("weather.ini", "User settings", "IPTWaccuWeatherLocation")
IPTWiPhoneTodayDisabled				= IniRead("weather.ini", "User settings", "IPTWiPhoneTodayDisabled")
IPTWlanguageFile					= IniRead("weather.ini", "User settings", "IPTWlanguageFile")
IPTWaccuWeatherIconPath				= IniRead("weather.ini", "User settings", "IPTWaccuWeatherIconPath")
IPTWforecastSkipIconFrequency		= IniRead("weather.ini", "User settings", "IPTWforecastSkipIconFrequency")
IPTWforecastSkipIconQuantity		= IniRead("weather.ini", "User settings", "IPTWforecastSkipIconQuantity")
IPTWforecastIconSkipFirstAtSunset	= IniRead("weather.ini", "User settings", "IPTWforecastIconSkipFirstAtSunset")
IPTWhideRealFeelInForecastIcons	= IniRead("weather.ini", "User settings", "IPTWhideRealFeelInForecastIcons")
IPTWwriteForecastInFile		= IniRead("weather.ini", "User settings", "IPTWwriteForecastInFile")

# ############################################################################
# configure location of parsed weather data in registry
IPTWregRoot					= IniRead("weather.ini", "Script settings", "IPTWregRoot")
IPTWappRegKey				= IniRead("weather.ini", "Script settings", "IPTWregKey")
IPTWregKey					= IniRead("weather.ini", "Script settings", "IPTWregKey") \ IPTWaccuWeatherLocation
IPTWgetAccuWeatherScript	= "getAccuWeather.mscr"

If (IPTWiPhoneTodayDisabled <> 1)
	If (NOT RegRead("HKLM", "Software\Microsoft\Today\Items\iPhoneToday", "Enabled") AND NOT ProcExists("iPhoneToday.exe") AND NOT ProcExists("iPhoneTodayDesktop.exe"))
		If (IPTWiPhoneTodayDisabled <> -1)
			IniWrite("weather.ini", "User settings", "IPTWiPhoneTodayDisabled", 1)
		EndIf
		IPTWiPhoneTodayDisabled = 1
	ElseIf (IPTWiPhoneTodayDisabled = -1)
		IPTWiPhoneTodayDisabled = 0
	EndIf
EndIf

isStarted = RegRead(IPTWregRoot, IPTWappRegKey, "Started")
If (isStarted = 0)
	goToAccuWeather = Question(IPTWgoToAccuWeatherQuestion)
	If (goToAccuWeather)
		defaultBrowser = Replace(Replace(RegRead("HKCR", "http\Shell\Open\Command", "Default"), " %1", ""), """", "")
		RunWait(defaultBrowser, "http://www.accuweather.com/world-index.asp?partner=rainmeter&traveler=0")
	EndIf
	RegWriteDWord(IPTWregRoot, IPTWappRegKey, "Started", 1)
	isShowingForecast = 1
Else
	isShowingForecast = RegRead(IPTWregRoot, IPTWappRegKey, "ShowingForecast")
EndIf
If (isStarted = 0 AND IPTWdownloadDataAfterStartOnIconTap)
	KillScript(IPTWgetAccuWeatherScript)
	CallScript(SystemPath("ScriptPath") \ "sys" \ IPTWgetAccuWeatherScript)
Else
	timeZone = RegRead(IPTWregRoot, IPTWregKey, "timeZone")
	deviceTimeZone = 60 * (TimeZoneBias() - 60)
	dataTimeZone = timeZone * 3600
	timeOffset = dataTimeZone - deviceTimeZone
	currentLocalTime = TimeStamp() + timeOffset
	If (isShowingForecast = 0 AND NOT IPTWiPhoneTodayDisabled)
		RegWriteDWord(IPTWregRoot, IPTWappRegKey, "ShowingForecast", 1)
		currentDate = FormatTime("m/d/Y", currentLocalTime)
		daysToSkip = 0
		skipMorning = 0
		foundDay = FALSE
		While (NOT foundDay AND RegValueExists(IPTWregRoot, IPTWregKey, "obsdate" & daysToSkip))
			obsdate = RegRead(IPTWregRoot, IPTWregKey, "obsdate" & daysToSkip)
			obsdate = SubStr("0" & Part(obsdate, "/", 1), -2) & "/" & SubStr("0" & Part(obsdate, "/", 2), -2) & "/" & Part(obsdate, "/", 3)
			If (obsdate eq currentDate)
				foundDay = TRUE
			Else
				daysToSkip = daysToSkip + 1
			EndIf
		EndWhile
		shownIcons = 0
		iconPositionOffset = 0
		If (foundDay)
			currenttime = Replace(FormatTime("A h:i", currentLocalTime), " 12", " 00")
			sunset = SubStr("0" & RegRead(IPTWregRoot, IPTWregKey, "sunset" & daysToSkip), -8)
			sunset = Replace(Part(sunset, " ", 2) & " " & Part(sunset, " ", 1), " 12", " 00")
			If (currenttime gt sunset)
				skipMorning = 1
				If (IPTWforecastIconSkipFirstAtSunset)
					IPTWforecastStartIcon = IPTWforecastStartIcon + 1
					shownIcons = 1
				EndIf
				iPhoneTodayPic = RegRead(IPTWregRoot, IPTWregKey, "iconN" & daysToSkip) & IPTWnightIconSuffix & "." & IPTWiconsExtension
				If (IPTWhideRealFeelInForecastIcons)
					iPhoneTodayTemp = weekday[RegRead(IPTWregRoot, IPTWregKey, "weekday" & daysToSkip)] & RegRead(IPTWregRoot, IPTWregKey, "loTemp" & daysToSkip)
				Else
					iPhoneTodayTemp = weekday[RegRead(IPTWregRoot, IPTWregKey, "weekday" & daysToSkip)] & RegRead(IPTWregRoot, IPTWregKey, "loTemp" & daysToSkip) & " (" & RegRead(IPTWregRoot, IPTWregKey, "loFeelTemp" & daysToSkip) & ")"
				EndIf
				Call("IPTWsetIcon", IPTWforecastStartPage, IPTWforecastStartIcon, iPhoneTodayTemp, iPhoneTodayPic, 0, "N", daysToSkip)
			Else
				iPhoneTodayPic = RegRead(IPTWregRoot, IPTWregKey, "iconD" & daysToSkip) & "." & IPTWiconsExtension
				If (IPTWhideRealFeelInForecastIcons)
					iPhoneTodayTemp = weekday[RegRead(IPTWregRoot, IPTWregKey, "weekday" & daysToSkip)] & RegRead(IPTWregRoot, IPTWregKey, "hiTemp" & daysToSkip)
				Else
					iPhoneTodayTemp = weekday[RegRead(IPTWregRoot, IPTWregKey, "weekday" & daysToSkip)] & RegRead(IPTWregRoot, IPTWregKey, "hiTemp" & daysToSkip) & " (" & RegRead(IPTWregRoot, IPTWregKey, "hiFeelTemp" & daysToSkip) & ")"
				EndIf
				Call("IPTWsetIcon", IPTWforecastStartPage, IPTWforecastStartIcon, iPhoneTodayTemp, iPhoneTodayPic, 0, "D", daysToSkip)
				iPhoneTodayPic = RegRead(IPTWregRoot, IPTWregKey, "iconN" & daysToSkip) & IPTWnightIconSuffix & "." & IPTWiconsExtension
				If (IPTWhideRealFeelInForecastIcons)
					iPhoneTodayTemp = weekday[RegRead(IPTWregRoot, IPTWregKey, "weekday" & daysToSkip)] & RegRead(IPTWregRoot, IPTWregKey, "loTemp" & daysToSkip)
				Else
					iPhoneTodayTemp = weekday[RegRead(IPTWregRoot, IPTWregKey, "weekday" & daysToSkip)] & RegRead(IPTWregRoot, IPTWregKey, "loTemp" & daysToSkip) & " (" & RegRead(IPTWregRoot, IPTWregKey, "loFeelTemp" & daysToSkip) & ")"
				EndIf
				Call("IPTWsetIcon", IPTWforecastStartPage, IPTWforecastStartIcon + 1, iPhoneTodayTemp, iPhoneTodayPic, 1, "N", daysToSkip)
			EndIf
			i = daysToSkip + 1
			time = Array("D", "N")
			tempType = Array("hi", "lo")
			iconSuffix = Array("", IPTWnightIconSuffix)
			While (RegValueExists(IPTWregRoot, IPTWregKey, "iconD" & i))
				For j = 1 to 2
					If (RegValueExists(IPTWregRoot, IPTWregKey, "icon" & time[j] & i))
						iconNum = 2 * (i - daysToSkip) + j - 1 - skipMorning
						iPhoneTodayPic = RegRead(IPTWregRoot, IPTWregKey, "icon" & time[j] & i) & iconSuffix[j] & "." & IPTWiconsExtension
						If (IPTWhideRealFeelInForecastIcons)
							iPhoneTodayTemp = weekday[RegRead(IPTWregRoot, IPTWregKey, "weekday" & i)] & RegRead(IPTWregRoot, IPTWregKey, tempType[j] & "Temp" & i)
						Else
							iPhoneTodayTemp = weekday[RegRead(IPTWregRoot, IPTWregKey, "weekday" & i)] & RegRead(IPTWregRoot, IPTWregKey, tempType[j] & "Temp" & i) & " (" & RegRead(IPTWregRoot, IPTWregKey, tempType[j] & "FeelTemp" & i) & ")"
						EndIf
						Call("IPTWsetIcon", IPTWforecastStartPage, IPTWforecastStartIcon + iconNum, iPhoneTodayTemp, iPhoneTodayPic, iconNum, time[j], i)
					EndIf
				Next
				i = i + 1
			EndWhile
		Else
			i = daysToSkip
		EndIf
		j = 1
		If (IPTWdisplayCityName)
			iconNum = 2 * (i - daysToSkip) + j - 1 - skipMorning
			locationCode = Replace(RegRead(IPTWregRoot, IPTWregKey, "locationCode"), "|", "_")
			cityIcon = IPTWaccuWeatherIconPath \ locationCode & "." & IPTWiconsExtension
			If (FileExists(cityIcon))
				iPhoneTodayPic = locationCode & "." & IPTWiconsExtension
			Else
				iPhoneTodayPic = IniRead("weather.ini", "User settings", "IPTWdefaultCityIcon")
			EndIf
			iPhoneTodayText = RegRead(IPTWregRoot, IPTWregKey, "city")
			Call("IPTWsetIcon", IPTWforecastStartPage, IPTWforecastStartIcon + iconNum, iPhoneTodayText, iPhoneTodayPic, iconNum, "", 0)
			j = j + 1
		EndIf
		If (IPTWdisplayObservationTime ne "")
			iconNum = 2 * (i - daysToSkip) + j - 1 - skipMorning
			iPhoneTodayPic = "AccuWeather." & IPTWiconsExtension
			iPhoneTodayText = IPTWdisplayObservationTime & RegRead(IPTWregRoot, IPTWregKey, "obstime")
			Call("IPTWsetIcon", IPTWforecastStartPage, IPTWforecastStartIcon + iconNum, iPhoneTodayText, iPhoneTodayPic, iconNum, "", 0)
		EndIf
		If (IPTWwriteForecastInFile)
			RegWriteDWord("HKLM", "\Software\iPhonetoday", "reloadIcon", 2)
		Else
			RegWriteDWord("HKLM", "\Software\iPhonetoday", "reloadIcon", 1)
		EndIf
	Else
		If (NOT IPTWiPhoneTodayDisabled)
			RegWriteDWord("HKLM", "\Software\iPhonetoday", "reloadIcons", 1)
		EndIf

		updateWeatherNow = FALSE
		time = RegRead(IPTWregRoot, IPTWregKey, "updateTime") + (IPTWforceDelayHours * 60 + IPTWforceDelayMinutes) * 60
		ctime = TimeStamp()
		If (time < ctime)
			If (IPTWupdateWeatherNowQuestion ne "")
				updateWeatherNow = Question(IPTWupdateWeatherNowQuestion)
			Else
				updateWeatherNow = TRUE
			EndIf
		EndIf

		CallScript(SystemPath("ScriptPath") \ "sys\waitForReadyIPT.mscr", 1)
		RegWriteDWord(IPTWregRoot, IPTWappRegKey, "ShowingForecast", 0)

		If (updateWeatherNow)
			KillScript(IPTWgetAccuWeatherScript)
			CallScript(SystemPath("ScriptPath") \ "sys" \ IPTWgetAccuWeatherScript)
		Else
			currentDate = FormatTime("m/d/Y", currentLocalTime)
			obsdate = RegRead(IPTWregRoot, IPTWregKey, "obsdate0")
			obsdate = SubStr("0" & Part(obsdate, "/", 1), -2) & "/" & SubStr("0" & Part(obsdate, "/", 2), -2) & "/" & Part(obsdate, "/", 3)
			If (obsdate eq currentDate)
				currenttime = Replace(FormatTime("A h:i", currentLocalTime), " 12", " 00")
				sunset = SubStr("0" & RegRead(IPTWregRoot, IPTWregKey, "sunset0"), -8)
				sunset = Replace(Part(sunset, " ", 2) & " " & Part(sunset, " ", 1), " 12", " 00")
				If (currenttime gt sunset)
					iconSuffix = IPTWnightIconSuffix
					suffix = "N"
				Else
					iconSuffix = ""
					suffix = "D"
				EndIf
				iPhoneTodayPic = RegRead(IPTWregRoot, IPTWregKey, "icon0") & iconSuffix & "." & IPTWiconsExtension
				iPhoneTodayTemp = RegRead(IPTWregRoot, IPTWregKey, "temp0") & " (" & RegRead(IPTWregRoot, IPTWregKey, "tempfeel0") & ")"
				CallScript(SystemPath("ScriptPath") \ "sys\IPTWreloadS2U2.mscr", 1)
				CallScript(SystemPath("ScriptPath") \ "sys\IPTWreloadIcon.mscr", IPTWcurrentWeatherPage, IPTWcurrentWeatherIcon, iPhoneTodayTemp, iPhoneTodayPic)
# no new data?
			Else
# If change of day, we retrieve previous night forecast as current condition
				CallScript(SystemPath("ScriptPath") \ "sys\IPTWuseObsoleteData.mscr")
			EndIf
		EndIf
	EndIf
EndIf

CallScript(SystemPath("ScriptPath") \ "sys" \ "waitForReadyIPT.mscr", 2)

Sub IPTWsetIcon
	If (NOT IPTWiPhoneTodayDisabled)
		If (shownIcons = IPTWforecastSkipIconFrequency)
			shownIcons = 0
			iconPositionOffset = iconPositionOffset + IPTWforecastSkipIconQuantity
		EndIf
		shownIcons = shownIcons + 1
		setIconPos = iconPositionOffset + argv[2]
		RegWriteDWord("HKLM", "\Software\iPhonetoday\icon" & argv[5], "nScreen", argv[1])
		RegWriteDWord("HKLM", "\Software\iPhonetoday\icon" & argv[5], "nIcon", setIconPos)
		RegWriteString("HKLM", "\Software\iPhonetoday\icon" & argv[5], "strName", argv[3])
		RegWriteString("HKLM", "\Software\iPhonetoday\icon" & argv[5], "strImage", IPTWaccuWeatherIconPath \ argv[4])
		RegWriteString("HKLM", "\Software\iPhonetoday\icon" & argv[5], "strExec", SystemPath("ScriptExe") \ "MortScript.exe")
		RegWriteString("HKLM", "\Software\iPhonetoday\icon" & argv[5], "strParameters", """" & SystemPath("ScriptPath") \ "sys\IPTWdisplayInfo.mscr"" dayOrNight=""" & argv[6] &""" dayNum=""" & argv[7] & """")
	EndIf
EndSub
