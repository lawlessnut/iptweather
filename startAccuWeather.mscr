IPTWscriptPath					= SystemPath("ScriptPath")
IPTWregRoot						= IniRead(IPTWscriptPath \ "weather.ini", "Script settings", "IPTWregRoot")
IPTWregKey						= IniRead(IPTWscriptPath \ "weather.ini", "Script settings", "IPTWregKey")
IPTWcurrentWeatherIcon			= IniRead(IPTWscriptPath \ "weather.ini", "User settings", "IPTWcurrentWeatherIcon")
IPTWcurrentWeatherPage			= IniRead(IPTWscriptPath \ "weather.ini", "User settings", "IPTWcurrentWeatherPage")
IPTWaccuWeatherIconPath			= IniRead(IPTWscriptPath \ "weather.ini", "User settings", "IPTWaccuWeatherIconPath")
IPTWiconsExtension				= IniRead(IPTWscriptPath \ "weather.ini", "User settings", "IPTWiconsExtension")
IPTWstartText					= IniRead(IPTWscriptPath \ "weather.ini", "User settings", "IPTWstartText")
IPTWscheduleNextUpdateOnStart	= IniRead(IPTWscriptPath \ "weather.ini", "User settings", "IPTWscheduleNextUpdateOnStart")
IPTWdelayHours					= IniRead(IPTWscriptPath \ "weather.ini", "User settings", "IPTWdelayHours")
IPTWdelayMinutes				= IniRead(IPTWscriptPath \ "weather.ini", "User settings", "IPTWdelayMinutes")
IPTWreloadLastDataOnStart		= IniRead(IPTWscriptPath \ "weather.ini", "User settings", "IPTWreloadLastDataOnStart")

# ############################################################################
# Set App-to-date version file path
versionFilePath = SystemPath("ScriptPath") \ "sys\version.xml"
RegWriteString("HKCU", "Software\MoDaCo\AppToDate\XML", "IPTWeather", versionFilePath)

# ############################################################################
# set up for next notification
If (IPTWscheduleNextUpdateOnStart AND (IPTWdelayHours <> 0 OR IPTWdelayMinutes <> 0))
	currentDate = FormatTime("m/d/Y")
	utime = TimeStamp() + (IPTWdelayHours * 60 + IPTWdelayMinutes) * 60
	mort = SystemPath("ScriptExe") \ "MortScript.exe"
	script = IPTWscriptPath \ "sys\getAccuWeather.mscr"
	RemoveNotifications(mort, """" & script & """")
	RunAt(utime, mort, """" & script & """")
EndIf

If (IPTWreloadLastDataOnStart = -1)
	RegWriteDWord(IPTWregRoot, IPTWregKey, "Started", 1)
EndIf

If (IPTWreloadLastDataOnStart = 1)
	RegWriteDWord(IPTWregRoot, IPTWregKey, "Started", 1)
	RegWriteDWord(IPTWregRoot, IPTWregKey, "ShowingForecast", 1)
	CallScript(IPTWscriptPath \ "iPhoneTodayAccuWeather.mscr")
EndIf

If (IPTWreloadLastDataOnStart = 0)
	If (NOT IniRead("weather.ini", "User settings", "IPTWiPhoneTodayDisabled"))
		iPhoneTodayPic = "AccuWeather." & IPTWiconsExtension
		RegWriteDWord("HKLM", "\Software\iPhonetoday\icon0", "nScreen", IPTWcurrentWeatherPage)
		RegWriteDWord("HKLM", "\Software\iPhonetoday\icon0", "nIcon", IPTWcurrentWeatherIcon)
		RegWriteString("HKLM", "\Software\iPhonetoday\icon0", "strName", IPTWstartText)
		RegWriteString("HKLM", "\Software\iPhonetoday\icon0", "strImage", IPTWaccuWeatherIconPath \ iPhoneTodayPic)
		RegWriteDWord("HKLM", "\Software\iPhonetoday", "reloadIcon", 1)
	EndIf
	RegWriteDWord(IPTWregRoot, IPTWregKey, "Started", 0)
EndIf

CallScript(SystemPath("ScriptPath") \ "sys" \ "waitForReadyIPT.mscr", 2)